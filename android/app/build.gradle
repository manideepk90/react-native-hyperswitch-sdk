apply plugin: "com.android.library"
apply plugin: "org.jetbrains.kotlin.android"
ext.generatePackageList = true
apply plugin: "com.facebook.react"


/**
 * This is the configuration block to customize your React Native Android app.
 * By default you don't need to apply any configuration, just uncomment the lines you need.
 */
react {
    /* Autolinking */
    autolinkLibrariesWithApp()
    // enableSoCleanup.set(true)  // Disabled to keep native libraries

    /* Folders */
    //   The root of your project, i.e. where "package.json" lives. Default is '../..'
    // root = file("../../")
    //   The folder where the react-native NPM package is. Default is ../../node_modules/react-native
    // reactNativeDir = file("../../node_modules/react-native")
    //   The folder where the react-native Codegen package is. Default is ../../node_modules/@react-native/codegen
    // codegenDir = file("../../node_modules/@react-native/codegen")
    //   The cli.js file which is the React Native CLI entrypoint. Default is ../../node_modules/react-native/cli.js
    // cliFile = file("../../node_modules/react-native/cli.js")

    /* Variants */
    //   The list of variants to that are debuggable. For those we're going to
    //   skip the bundling of the JS bundle and the assets. By default is just 'debug'.
    //   If you add flavors like lite, prod, etc. you'll have to list your debuggableVariants.
    // debuggableVariants = ["liteDebug", "prodDebug"]

    /* Bundling */
    //   A list containing the node command and its flags. Default is just 'node'.
    // nodeExecutableAndArgs = ["node"]
    //
    //   The command to run when bundling. By default is 'bundle'
    // bundleCommand = "ram-bundle"
    //
    //   The path to the CLI configuration file. Default is empty.
    // bundleConfig = file(../rn-cli.config.js)
    //
    //   The name of the generated asset file containing your JS bundle
     bundleAssetName = "hyperswitch.bundle"
    //
    //   The entry file for bundle generation. Default is 'index.android.js' or 'index.js'
    // entryFile = file("../js/MyApplication.android.js")
    //
    //   A list of extra flags to pass to the 'bundle' commands.
    //   See https://github.com/react-native-community/cli/blob/main/docs/commands.md#bundle
    // extraPackagerArgs = []

    /* Hermes Commands */
    //   The hermes compiler command to run. By default it is 'hermesc'
    // hermesCommand = "$rootDir/my-custom-hermesc/bin/hermesc"
    //
    //   The list of flags to pass to the Hermes compiler. By default is "-O", "-output-source-map"
    // hermesFlags = ["-O", "-output-source-map"]
}


/**
 * Set this to true to Run Proguard on Release builds to minify the Java bytecode.
 */
def enableProguardInReleaseBuilds = false

/**
 * The preferred build flavor of JavaScriptCore (JSC)
 *
 * For example, to use the international variant, you can use:
 * `def jscFlavor = io.github.react-native-community:jsc-android-intl:2026004.+`
 *
 * The international variant includes ICU i18n library and necessary data
 * allowing to use e.g. `Date.toLocaleString` and `String.localeCompare` that
 * give correct results when using with locales other than en-US. Note that
 * this variant is about 6MiB larger per architecture than default.
 */
def jscFlavor = 'io.github.react-native-community:jsc-android:2026004.+'

android {
    ndkVersion rootProject.ext.ndkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    compileSdk rootProject.ext.compileSdkVersion

    namespace "io.hyperswitch"
    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
        ndk {
            abiFilters "armeabi-v7a", "arm64-v8a"  // Remove x86, x86_64 if not needed
        }

    }
    signingConfigs {
        debug {
            storeFile file('debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
        release {
            ndk {
                debugSymbolLevel 'NONE'  // Remove debug symbols to reduce .so file size
            }
            shrinkResources false

            // Caution! In production, you need to generate your own keystore file.
            // see https://reactnative.dev/docs/signed-apk-android.
            signingConfig signingConfigs.debug
            minifyEnabled enableProguardInReleaseBuilds
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }

    packagingOptions {
        // Use pickFirst instead of exclude to handle duplicates
        pickFirst "**/libc++_shared.so"
        pickFirst "**/libfbjni.so"
        pickFirst "**/libjsi.so"
        pickFirst "**/libhermes.so"
        pickFirst "**/libreactnative.so"
        pickFirst "**/libimagepipeline.so"
        pickFirst "**/libnative-filters.so"
        pickFirst "**/libnative-imagetranscoder.so"

        // Also add these common React Native duplicates
        pickFirst "**/libreactnativejni.so"
        pickFirst "**/libturbomodulejsijni.so"
        pickFirst "**/libappmodules.so"
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
        }
    }

}

dependencies {
    // The version of react-native is set by the React Native Gradle Plugin
    api("com.facebook.react:react-android")
    api project(':hyperswitch-sdk-android-api')
    implementation project(':hyperswitch-sdk-android-common')
    implementation project(':hyperswitch-sdk-android-logger')
//    implementation project(':hyperswitch-sdk-android-hyperota')
    if (hermesEnabled.toBoolean()) {
        api("com.facebook.react:hermes-android")
    } else {
        api jscFlavor
    }
}


rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
        maven {
            url("$rootDir/maven/")
        }
        maven {
            url "https://jitpack.io"
        }
        maven {
            url("$rootDir/../node_modules/detox/Detox-android")
        }
        maven {
            url "https://maven.juspay.in/hyper-sdk/"
        }
    }
}

apply plugin: "maven-publish"
def runtimeDeps = [
        'react-native-inappbrowser-reborn',
        'react-native-svg',
        'sentry_react-native',
]

def runtimeDepsExcluded = [
        'demo-app',
        'demo-app-lite'
]

def runtimeDepsOptional = [
        'react-native-hyperswitch-kount',
        'react-native-hyperswitch-paypal',
        'juspay-tech_react-native-hyperswitch-netcetera-3ds',
        'juspay-tech_react-native-hyperswitch-samsung-pay',
        'juspay-tech_react-native-hyperswitch-scancard',
        'react-native-klarna-inapp-sdk',
]

def configurePomMetadata = { root ->
    root.appendNode('name', 'Hyperswitch SDK for Android')
    root.appendNode('description', 'This is the Hyperswitch SDK for Android, providing seamless integration with the Hyperswitch platform.')
    root.appendNode('url', 'http://www.hyperswitch.io')

    def licenseNode = root.appendNode('licenses').appendNode('license')
    licenseNode.appendNode('name', 'The Apache License, Version 2.0')
    licenseNode.appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.txt')
    licenseNode.appendNode('distribution', 'repo')

    def scmNode = root.appendNode('scm')
    scmNode.appendNode('url', 'https://github.com/hyperswitch/hyperswitch-sdk-android')
    scmNode.appendNode('connection', 'scm:git:git://github.com/juspay/hyperswitch-sdk-android.git')
    scmNode.appendNode('developerConnection', 'scm:git:ssh://git@github.com:juspay/hyperswitch-sdk-android.git')
    scmNode.appendNode('tag', 'HEAD')

    def developerNode = root.appendNode('developers').appendNode('developer')
    developerNode.appendNode('id', 'sh-iv-am')
    developerNode.appendNode('name', 'Shivam')
    developerNode.appendNode('email', 'shivam.shashank@juspay.in')
}

gradle.projectsEvaluated {
    subprojects {
        def configurePom = { pom ->
            def root = pom.asNode()
            configurePomMetadata(root)

            def dependencies = root.appendNode('dependencies')
            configurations.getByName('releaseCompileClasspath').getResolvedConfiguration().getFirstLevelModuleDependencies().each {
                if (!runtimeDepsOptional.contains(it.moduleName)) {
                    def dependency = dependencies.appendNode('dependency')
                    dependency.appendNode('groupId', it.moduleGroup)
                    dependency.appendNode('artifactId', it.moduleName)
                    dependency.appendNode('version', runtimeDeps.contains(it.moduleName) ? rnlibVersion : it.moduleVersion)
                }
            }
        }

        def androidSourcesJar = tasks.register('androidSourcesJar', Jar) {
            archiveClassifier.set('sources')
            from android.sourceSets.main.java.srcDirs
            from android.sourceSets.main.kotlin.srcDirs
        }

        publishing {
            publications {
                if (!runtimeDepsExcluded.contains(project.name)) {
                    "$project.name"(MavenPublication) {
                        groupId = project.group
                        artifactId = project.name == 'app' ? 'hyperswitch-sdk-android' : project.name
                        version = runtimeDeps.contains(project.name) || runtimeDepsOptional.contains(project.name) ? rnlibVersion : project.version
                        artifact "$buildDir/outputs/aar/$project.name-release.aar"
                        artifact androidSourcesJar
                        pom.withXml(configurePom)
                    }
                }
            }
            repositories {
                maven {
                    url("${project.rootDir}/maven")
                }
            }
        }
    }
}

android.libraryVariants.all { variant ->
    if (variant.name == "release") {
        variant.packageLibraryProvider.get().doLast {
            // Find NDK strip tool
            def ndkDir = null
            def localProps = new Properties()
            def localPropsFile = rootProject.file("local.properties")
            if (localPropsFile.exists()) {
                localPropsFile.withInputStream { localProps.load(it) }
                def sdkDir = localProps.getProperty("sdk.dir")
                if (sdkDir) {
                    def ndkPath = new File(sdkDir, "ndk/${rootProject.ext.ndkVersion}")
                    if (ndkPath.exists()) {
                        ndkDir = ndkPath.absolutePath
                    }
                }
            }

            if (!ndkDir) {
                println "Could not find NDK directory. Skipping stripping."
                return
            }

            def os = System.getProperty("os.name").toLowerCase()
            def arch = System.getProperty("os.arch").toLowerCase()

            // Detect correct strip tool path
            def stripTool
            if (os.contains("mac")) {
                if (arch.contains("aarch64") || arch.contains("arm64")) {
                    stripTool = "${ndkDir}/toolchains/llvm/prebuilt/darwin-arm64/bin/llvm-strip"
                } else {
                    stripTool = "${ndkDir}/toolchains/llvm/prebuilt/darwin-x86_64/bin/llvm-strip"
                }
            } else {
                stripTool = "${ndkDir}/toolchains/llvm/prebuilt/linux-x86_64/bin/llvm-strip"
            }

            // Fallback to find strip tool
            if (!file(stripTool).exists()) {
                def possiblePaths = [
                    "${ndkDir}/toolchains/llvm/prebuilt/darwin-arm64/bin/llvm-strip",
                    "${ndkDir}/toolchains/llvm/prebuilt/darwin-x86_64/bin/llvm-strip",
                    "${ndkDir}/toolchains/llvm/prebuilt/linux-x86_64/bin/llvm-strip"
                ]
                for (path in possiblePaths) {
                    if (file(path).exists()) {
                        stripTool = path
                        break
                    }
                }
            }

            if (!file(stripTool).exists()) {
                println "Strip tool not found at: ${stripTool}. Skipping stripping."
                return
            }

            println "Using strip tool: ${stripTool}"

            // Strip all .so files in intermediates
            fileTree("${buildDir}/intermediates/stripped_native_libs/release/out/lib").visit { FileVisitDetails details ->
                if (details.file.name.endsWith('.so')) {
                    def originalSize = details.file.length()
                    def process = new ProcessBuilder(stripTool, '--strip-all', details.file.absolutePath)
                        .redirectErrorStream(true)
                        .start()
                    process.waitFor()
                    def newSize = details.file.length()
                    if (newSize < originalSize) {
                        def saved = (originalSize - newSize) / 1024 / 1024
                        println "Stripped ${details.file.name}: ${String.format("%.2f", originalSize / 1024 / 1024)}MB -> ${String.format("%.2f", newSize / 1024 / 1024)}MB (saved ${String.format("%.2f", saved)}MB)"
                    }
                }
            }

            // Also strip in the final AAR output
            def aarFile = file("${buildDir}/outputs/aar/${project.name}-release.aar")
            if (aarFile.exists()) {
                def tempDir = file("${buildDir}/tmp/aar-extract")
                tempDir.mkdirs()

                copy {
                    from zipTree(aarFile)
                    into tempDir
                }

                fileTree(tempDir).visit { FileVisitDetails details ->
                    if (details.file.name.endsWith('.so')) {
                        def originalSize = details.file.length()
                        def process = new ProcessBuilder(stripTool, '--strip-all', details.file.absolutePath)
                            .redirectErrorStream(true)
                            .start()
                        process.waitFor()
                        def newSize = details.file.length()
                        if (newSize < originalSize) {
                            def saved = (originalSize - newSize) / 1024 / 1024
                            println "Stripped AAR ${details.file.name}: ${String.format("%.2f", originalSize / 1024 / 1024)}MB -> ${String.format("%.2f", newSize / 1024 / 1024)}MB (saved ${String.format("%.2f", saved)}MB)"
                        }
                    }
                }

                ant.zip(destfile: aarFile, basedir: tempDir)
                delete tempDir
            }
        }
    }
}